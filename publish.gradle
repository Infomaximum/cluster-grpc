task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

task packageJavadoc(type: Jar) {
    from sourceSets.main.java.srcDirs
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java
            artifact sourceJar
            artifact packageJavadoc

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'cluster-grpc'
                description = 'Library for creating a light cluster on transport grpc'
                url = 'https://github.com/Infomaximum/cluster-grpc'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'infomaximum'
                        name = 'Infomaximum LLC'
                        email = 'develop@infomaximum.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Infomaximum/cluster-grpc.git'
                    developerConnection = 'scm:git:git@github.com:Infomaximum/cluster-grpc.git'
                    url = 'https://github.com/Infomaximum/cluster-grpc'
                }
            }
            pom.withXml {//Т.к. у нас толстая банка - удаляем все зависимости их pom.xml, и добавляем необходимые
                asNode().remove(asNode().get('dependencies'))
                def dependenciesNode = asNode().appendNode('dependencies')

                def dependencyNodeAnnotationApi = dependenciesNode.appendNode('dependency')
                dependencyNodeAnnotationApi.appendNode('groupId', 'javax.annotation')
                dependencyNodeAnnotationApi.appendNode('artifactId', 'javax.annotation-api')
                dependencyNodeAnnotationApi.appendNode('version', '1.3.2')
                dependencyNodeAnnotationApi.appendNode('scope', 'runtime')

                def dependencyNodeGuava = dependenciesNode.appendNode('dependency')
                dependencyNodeGuava.appendNode('groupId', 'com.google.guava')
                dependencyNodeGuava.appendNode('artifactId', 'guava')
                dependencyNodeGuava.appendNode('version', '32.1.3-jre')
                dependencyNodeGuava.appendNode('scope', 'runtime')
                def dependencyNodeGuavaExclusions = dependencyNodeGuava.appendNode('exclusions')
                def dependencyNodeGuavaExclusionJ2objc = dependencyNodeGuavaExclusions.appendNode('exclusion')
                dependencyNodeGuavaExclusionJ2objc.appendNode('groupId', 'com.google.j2objc')
                dependencyNodeGuavaExclusionJ2objc.appendNode('artifactId', 'j2objc-annotations')
                def dependencyNodeGuavaExclusionFindbugs = dependencyNodeGuavaExclusions.appendNode('exclusion')
                dependencyNodeGuavaExclusionFindbugs.appendNode('groupId', 'com.google.code.findbugs')
                dependencyNodeGuavaExclusionFindbugs.appendNode('artifactId', 'jsr305')
                def dependencyNodeGuavaExclusionErrorprone = dependencyNodeGuavaExclusions.appendNode('exclusion')
                dependencyNodeGuavaExclusionErrorprone.appendNode('groupId', 'com.google.errorprone')
                dependencyNodeGuavaExclusionErrorprone.appendNode('artifactId', 'error_prone_annotations')
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}